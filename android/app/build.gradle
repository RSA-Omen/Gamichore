apply plugin: 'com.android.application'

def releaseStoreFile = System.getenv('GAMICHORE_UPLOAD_STORE_FILE')
def releaseStorePassword = System.getenv('GAMICHORE_UPLOAD_STORE_PASSWORD')
def releaseKeyAlias = System.getenv('GAMICHORE_UPLOAD_KEY_ALIAS')
def releaseKeyPassword = System.getenv('GAMICHORE_UPLOAD_KEY_PASSWORD')
def releaseSigningReady = releaseStoreFile && releaseStorePassword && releaseKeyAlias && releaseKeyPassword

def appVersionCodeEnv = System.getenv('GAMICHORE_VERSION_CODE')
def appVersionNameEnv = System.getenv('GAMICHORE_VERSION_NAME')
def appVersionCode = (appVersionCodeEnv ?: '1') as Integer
def appVersionName = appVersionNameEnv ?: '1.0.0'

android {
    namespace "com.gamichore.app"
    compileSdk rootProject.ext.compileSdkVersion
    defaultConfig {
        applicationId "com.gamichore.app"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode appVersionCode
        versionName appVersionName
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        aaptOptions {
             // Files and dirs to omit from the packaged assets dir, modified to accommodate modern web apps.
             // Default: https://android.googlesource.com/platform/frameworks/base/+/282e181b58cf72b6ca770dc7ca5f91f135444502/tools/aapt/AaptAssets.cpp#61
            ignoreAssetsPattern '!.svn:!.git:!.ds_store:!*.scc:.*:!CVS:!thumbs.db:!picasa.ini:!*~'
        }
    }
    signingConfigs {
        release {
            if (releaseSigningReady) {
                storeFile file(releaseStoreFile)
                storePassword releaseStorePassword
                keyAlias releaseKeyAlias
                keyPassword releaseKeyPassword
            }
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            if (releaseSigningReady) {
                signingConfig signingConfigs.release
            }
        }
    }
}

gradle.taskGraph.whenReady { taskGraph ->
    def runningRelease = taskGraph.allTasks.any { it.name.toLowerCase().contains('release') }
    if (runningRelease && !releaseSigningReady) {
        throw new GradleException(
            "Missing release signing env vars. Set GAMICHORE_UPLOAD_STORE_FILE, " +
            "GAMICHORE_UPLOAD_STORE_PASSWORD, GAMICHORE_UPLOAD_KEY_ALIAS, and GAMICHORE_UPLOAD_KEY_PASSWORD."
        )
    }
}

repositories {
    flatDir{
        dirs '../capacitor-cordova-android-plugins/src/main/libs', 'libs'
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation "androidx.appcompat:appcompat:$androidxAppCompatVersion"
    implementation "androidx.coordinatorlayout:coordinatorlayout:$androidxCoordinatorLayoutVersion"
    implementation "androidx.core:core-splashscreen:$coreSplashScreenVersion"
    implementation project(':capacitor-android')
    testImplementation "junit:junit:$junitVersion"
    androidTestImplementation "androidx.test.ext:junit:$androidxJunitVersion"
    androidTestImplementation "androidx.test.espresso:espresso-core:$androidxEspressoCoreVersion"
    implementation project(':capacitor-cordova-android-plugins')
}

apply from: 'capacitor.build.gradle'

try {
    def servicesJSON = file('google-services.json')
    if (servicesJSON.text) {
        apply plugin: 'com.google.gms.google-services'
    }
} catch(Exception e) {
    logger.info("google-services.json not found, google-services plugin not applied. Push Notifications won't work")
}
